⍝ Enclose_with_axis.tc
⍝ ----------------------------------

⍝ ----------------------------------

      A←2 3ρι6
      A
1 2 3
4 5 6
      Z←⊂[1]A
      Z
 1 4  2 5  3 6 
      ρZ
3
      ρ¨Z
 2  2  2 
      ≡Z
2

      Y←⊂[2]A
      Y
 1 2 3  4 5 6 
      ρY
2
      ρ¨Y
 3  3 
      ≡Y
2

      B←3 4ρ'PINEODORDATA'
      B
PINE
ODOR
DATA
      X←⊂[1]B
      X
 POD IDA NOT ERA 
      ρX
4
      ρ¨X
 3  3  3  3 
      ≡X
2

      W←⊂[2]B
      W
 PINE ODOR DATA 
      ρW
3
      ρ¨W
 4  4  4 
      ≡W
2

⍝ empty axis ----------------------------------

      C←2 3ρι6
      V←⊂[ι0]C
      V
1 2 3
4 5 6
      ρV
2 3
      ≡V
1

      Q←2 3ρ'CAT' 'DOG' 'FOX' 'COW' 'BAT' 'YAK'
      Q
 CAT DOG FOX 
 COW BAT YAK 
      ρQ
2 3
      ≡Q
2

      H←⊂[ι0]Q
      H
  CAT   DOG   FOX  
  COW   BAT   YAK  
      ρH
2 3
      ≡H
3

⍝ order of axes ----------------------------------

      S←2 3 4ρ'LESSSOMENONEMOREMANYMOST'
      S
LESS
SOME
NONE

MORE
MANY
MOST
      P←⊂[2 3]S
      P
 LESS  MORE 
 SOME  MANY 
 NONE  MOST 
      ρP
2
      ρ¨P
 3 4  3 4 

      ≡P
2
      Q←⊂[3 2]S
      Q
 LSN  MMM 
 EOO  OAO 
 SMN  RNS 
 SEE  EYT 
      ρQ
2
      ρ¨Q
 4 3  4 3 
      ≡Q
2

⍝ all axes in X ----------------------------------

      T←2 3 4ρι24
      T
 1  2  3  4
 5  6  7  8
 9 10 11 12

13 14 15 16
17 18 19 20
21 22 23 24
      ρT
2 3 4
      J←⊂[1 2 3]T
      J
  1  2  3  4 
  5  6  7  8 
  9 10 11 12 

 13 14 15 16 
 17 18 19 20 
 21 22 23 24 

      ≡J
2

      ⊂[1 3 2]T
  1  5  9 
  2  6 10 
  3  7 11 
  4  8 12 

 13 17 21 
 14 18 22 
 15 19 23 
 16 20 24 

      (⊂[1 3 2]T)≡⊂(⍋ 1 3 2)⍉T
1

