⍝ Ravel_with_axis.tc

⍝ X is a fraction ----------------------------------

      A←2 3ρ'TENSIX'
      A
TEN
SIX

      Z←,[.1]A
      Z
TEN
SIX

      ρZ
1 2 3


      Y←,[1.1]A
      Y
TEN

SIX

      ρY
2 1 3


      W←,[2.1]A
      W
T
E
N

S
I
X

      ρW
2 3 1


      B←10 15 20
      V←,[1.1]B
      V
10
15
20

      ρV
3 1

⍝ X is an integer ----------------------------------

      C←3 2 4ρι24
      C
 1  2  3  4
 5  6  7  8

 9 10 11 12
13 14 15 16

17 18 19 20
21 22 23 24

      P←,[2 3]C
      P
 1  2  3  4  5  6  7  8
 9 10 11 12 13 14 15 16
17 18 19 20 21 22 23 24

      ρP
3 8

      J←,[1 2]C
      J
 1  2  3  4
 5  6  7  8
 9 10 11 12
13 14 15 16
17 18 19 20
21 22 23 24

      ρJ
6 4


      A←'ANT' 'BOAR' 'CAT' 'DOG' 'ELK' 'FOX' 'GNU'
      B←'HEN' 'IBEX' 'JIRD' 'KITE' 'LAMB' 'MICE'
      C←'NENE' 'OX' 'PIG' 'QUAIL' 'RAT' 'SEAL'
      D←4 2 3ρA,B,C,'TITI' 'VIPER' 'WOLF' 'YAK' 'ZEBRA'
      D
 ANT  BOAR  CAT   
 DOG  ELK   FOX   

 GNU  HEN   IBEX  
 JIRD KITE  LAMB  

 MICE NENE  OX    
 PIG  QUAIL RAT   

 SEAL TITI  VIPER 
 WOLF YAK   ZEBRA 

      ρD
4 2 3

      ≡D
2

      M←,[1 2]D
      M
 ANT  BOAR  CAT   
 DOG  ELK   FOX   
 GNU  HEN   IBEX  
 JIRD KITE  LAMB  
 MICE NENE  OX    
 PIG  QUAIL RAT   
 SEAL TITI  VIPER 
 WOLF YAK   ZEBRA 

      ρM
8 3

      ≡M
2

⍝ X is empty ----------------------------------

      H←2 3ρι6
      N←,[ι0]H
      N
1
2
3

4
5
6

      ≡N
1

      K←'PRUNE' 'PEAR' 'FIG'
      ρK
3

      ≡K
2

      I←,[ι0]K
      I
 PRUNE 
 PEAR  
 FIG   

      ρI
3 1
      ≡I
2

⍝ Turn an array into a matrix ----------------------------------

      E←3 2 5ρι30
      ,[ιρρE],[.5]E
 1  2  3  4  5
 6  7  8  9 10
11 12 13 14 15
16 17 18 19 20
21 22 23 24 25
26 27 28 29 30


      G←'JIM' 'ED' 'MIKE'
      ρG
3

      F←,[ιρρG],[.5]G
      F
 JIM ED MIKE 

      ρF
1 3

⍝ selective spec ----------------------------------

      Q←2 3 4ρι24
      (,[2 3]Q)←2 12ρ-ι24
      Q
 ¯1  ¯2  ¯3  ¯4
 ¯5  ¯6  ¯7  ¯8
 ¯9 ¯10 ¯11 ¯12

¯13 ¯14 ¯15 ¯16
¯17 ¯18 ¯19 ¯20
¯21 ¯22 ¯23 ¯24

      ρQ
2 3 4

      ⍝ regression
      ⍝
      ,[3 4] 2 4 4⍴1
AXIS ERROR
      ,[3 4]2 4 4⍴1
      ^    ^

      )SIC

⍝ ==================================

