⍝ Disclose.tc
⍝ ----------------------------------

⍝ same shapes ----------------------------------

      ⊃(1 2 3)(4 5 6)
1 2 3
4 5 6
      ⊃(2 3 4)(5 6)
2 3 4
5 6 0
      R←2 3ρ(ι4)'ABCD' '****'(5 6 7 8)'EFGH' '∆∆∆∆'
      R
 1 2 3 4  ABCD **** 
 5 6 7 8  EFGH ∆∆∆∆ 
      ρR
2 3
      ≡R
2
      Z←⊃R
      Z
1 2 3 4
A B C D
* * * *

5 6 7 8
E F G H
∆ ∆ ∆ ∆
      ρZ
2 3 4
      ≡Z
1

⍝ shapes differ ----------------------------------

      E←(2 4ρι8) 9 (3 2ρ'ABCDEF')
      E
 1 2 3 4   9  AB 
 5 6 7 8      CD 
              EF 
      N←⊃E
      N
1 2 3 4
5 6 7 8
0 0 0 0

9 0 0 0
0 0 0 0
0 0 0 0

A B    
C D    
E F    
      ρN
3 3 4
      ≡N
1

⍝ wheel of fortune ----------------------------------

      D←'WHEEL' 'OF' 'FORTUNE'
      V←⊃D
      V
WHEEL  
OF     
FORTUNE
      ρV
3 7
      ≡V
1

⍝ empty value

      ''≡⊃⊂''
1

