⍝ Disclose_with_axis.tc
⍝ ----------------------------------

⍝ ----------------------------------

      H←'ABCD' (1 2 3 4) 'WXYZ'
      Z←⊃[1]H
      Z
A 1 W
B 2 X
C 3 Y
D 4 Z
      ρZ
4 3
      ≡Z
1
      W←⊃[2]H
      W
A B C D
1 2 3 4
W X Y Z
      ρW
3 4
      ≡W
1

⍝ ----------------------------------

      Z←2 4ρ'PA' 'MA' 'WE' 'BY' 'IT' 'UP' 'ON' 'HI'
      R←(2 4ρι8) (2 4ρ'ABCDEFGH') (Z)
      B←⊃[1 2]R
      ρB
2 4 3
      B
 1 A PA 
 2 B MA 
 3 C WE 
 4 D BY 

 5 E IT 
 6 F UP 
 7 G ON 
 8 H HI 

      ≡B
2
      Y←⊃[1 3]R
      ρY
2 3 4
      Y
  1  2  3  4 
  A  B  C  D 
 PA MA WE BY 

  5  6  7  8 
  E  F  G  H 
 IT UP ON HI 

      V←⊃[2 3]R
      ρV
3 2 4

      V
  1  2  3  4 
  5  6  7  8 

  A  B  C  D 
  E  F  G  H 

 PA MA WE BY 
 IT UP ON HI 

⍝ order of axes matters ----------------------------------

      N←⊃[2 1]R
      N
 1 A PA 
 5 E IT 

 2 B MA 
 6 F UP 

 3 C WE 
 7 G ON 

 4 D BY 
 8 H HI 

      M←⊃[3 1]R
      M
  1  5 
  A  E 
 PA IT 

  2  6 
  B  F 
 MA UP 

  3  7 
  C  G 
 WE ON 

  4  8 
  D  H 
 BY HI 

      P←⊃[3 2]R
      P
  1  5 
  2  6 
  3  7 
  4  8 

  A  E 
  B  F 
  C  G 
  D  H 

 PA IT 
 MA UP 
 WE ON 
 BY HI 

⍝ shapes differ ----------------------------------

      Q←(ι3) 'JUMP'
      N←⊃[1]Q
      N
1 J
2 U
3 M
0 P
      E←(ι5) 'JUMP'
      J←⊃[1]E
      J
1 J
2 U
3 M
4 P
5  

      S←(2 6ρ'ABCDEFGHIJKL') (3 4ρι12)
      S
 ABCDEF   1  2  3  4 
 GHIJKL   5  6  7  8 
          9 10 11 12 
      ρS
2
      ρ¨S
 2 6  3 4 
      ⌈/(ρ¨S)∼⊃ι0
 3 6 
      D←⊃[2 3]S
      ρD
2 3 6
      D
A  B  C  D E F
G  H  I  J K L
              

1  2  3  4 0 0
5  6  7  8 0 0
9 10 11 12 0 0

⍝ empty axis needed ----------------------------------

      T←⊂¨'ONE' 'FOUR' 'THREE'
      ≡¨T
2 2 2
      ⊃[ι0]T
 ONE FOUR THREE 
