⍝ Drop.tc
⍝ ----------------------------------

⍝ ----------------------------------

      3↓12 31 45 10 57
10 57
      ¯3↓12 31 45 10 57
12 31

⍝ nonscalar right argument ----------------------------------

      A←3 5ρ'STRIPERODEPLANT'
      B←'STOREFIRSTMIGHTHATER'
      B←B,'SHEETTHEREMETROERASE'
      B←3 4 5ρB,'BREADOTHERANVILEVADE'
      A
STRIP
ERODE
PLANT
      1 2↓A
ODE
ANT

      B
STORE
FIRST
MIGHT
HATER

SHEET
THERE
METRO
ERASE

BREAD
OTHER
ANVIL
EVADE

      C←¯1 2 ¯2
      C↓B
MIG
HAT

MET
ERA

      K←3 2 4ρ'ABCDEFGH',(ι8),'abcdefgh'
      K
A B C D
E F G H

1 2 3 4
5 6 7 8

a b c d
e f g h

      Z←2 1 3↓K
      Z
h
      ρZ
1 1 1

⍝ dropping none ----------------------------------

      0↓'INTACT'
INTACT
      0 2↓3 5ρι15
 3  4  5
 8  9 10
13 14 15

⍝ overdrop ----------------------------------

      W←5↓23 41 73 26
      ρW
0

      H←2 3ρ'ABCDEF'
      Y←3 1↓H
      ρY
0 2
      M←2 3↓H
      ρM
0 0

⍝ scalar right argument ----------------------------------

      J←0↓4
      J
4
      ρJ
1
      K←0 0 0↓4
      K
4
      ρK
1 1 1

⍝ effect on depth ----------------------------------

      D←'A' 'AN'('ANT' 'ANTE')
      D
 A AN   ANT ANTE  
      ρD
3
      ≡D
3
      S←¯1↓D
      S
 A AN 
      ≡S
2
      T←¯2↓D
      T
A
      ρT
1
      ≡T
1

⍝ selective spec ----------------------------------


      U←'ABCDE'
      (2↓U)←ι3
      U
AB 1 2 3

      V←3 4ρ'ABCDEFGHIJKL'
      V
ABCD
EFGH
IJKL

      (1 ¯1↓V)←2 3ρι6
      V
A B C D
1 2 3 H
4 5 6 L
