⍝ Drop_with_axis.tc
⍝ ----------------------------------

⍝ ----------------------------------

      A←3 4ρ'FOLDBEATRODE'
      A
FOLD
BEAT
RODE
      1↓[1]A
BEAT
RODE
      1 0↓A
BEAT
RODE
      1↓[2]A
OLD
EAT
ODE
      0 1↓A
OLD
EAT
ODE

⍝ Permitted axes ----------------------------------

      Q←3 2 4ρ'ABCDEFGH',(ι8),'abcdefgh'
      Q
A B C D
E F G H

1 2 3 4
5 6 7 8

a b c d
e f g h

      1 ¯1↓[2 3]Q
E F G

5 6 7

e f g
      1 ¯1↓[3 2]Q
B C D

2 3 4

b c d

⍝ Effect on Depth ----------------------------------

      T←'W' 'WE'('WEE' 'WEED')'B' 'BE'('BEE' 'BEEP')

      U←2 3ρT
      U
 W WE   WEE WEED  
 B BE   BEE BEEP  
      ≡U
3
      Q←1↓[1]U
      ≡Q
3
      Q
 B BE   BEE BEEP  
      ρQ
1 3

      M←¯1↓[2]U
      ≡M
2
      M
 W WE 
 B BE 
      ρM
2 2
      N←¯2↓[2]U
      ≡N
1
      N
W
B
      ρN
2 1

⍝ Selective specification ----------------------------------

      V←3 4ρ'ABCDEFGHIJKL'
      V
ABCD
EFGH
IJKL
      (1↓[1]V)←2 4ρι8
      V
A B C D
1 2 3 4
5 6 7 8

