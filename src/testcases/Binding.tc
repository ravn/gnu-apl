⍝ Binding.tc
⍝ ----------------------------------

      ⍝ Vector notation vs. left function arg
      ⍝
      A←1 2 3
      A 4 + 10
 11 12 13  14 
      (A 4) + 10
 11 12 13  14 
      A (4 + 10)
 1 2 3  14 

      A←B←C←1 2 3
      A
1 2 3
      B
1 2 3
      C
1 2 3

⍝ dyadic operator ----------------------------------

∇z←(f glue l) r
 ⍝ From Intro to APL2, p72
 z←l f r
∇

∇z←(f AND g) r
 ⍝From Intro to APL p72
 z←(f r)(g r)
∇

      (*glue 2) AND+ 0 1 2
 1 2 4  0 1 2 

      *glue 2 AND+ 0 1 2
 1 2 4  0 1 2 

      )ERASE glue AND

⍝ bracked index and function axis

∇Z←A DyMon B
⊃('Monadic' 'Dyadic')[Z←,1+0≠⎕NC 'A']
∇

      +/ DyMon 1
Monadic
1

      +/[1] DyMon 1
Monadic
1

      (+/[1]) DyMon 1
Monadic
1

      (1 2 3)[1] DyMon 1
Dyadic
2

⍝ selective spec ----------------------------------

      D←(A B C)←4 5 6
      A
4
      B
5
      C
6
      D
4 5 6

      D←(A B C)←⊂4 5 6
      A
4 5 6
      B
4 5 6
      C
4 5 6
      D
 4 5 6 

      ⍝ regression
      ⍝ 
      ≡⊂⍣4⍣6⊢'hello'
25
      ≡(⊂⍣4)⍣6⊢'hello'
25
      ≡⊂⍣(4⍣6)⊢'hello'
DOMAIN ERROR
      ≡⊂⍣(4⍣6)⊢'hello'
       ^      ^

      ⎕FX'R←(F L G)Y' 'R←F Y'
L
      ⎕FX'R←(F R G)Y' 'R←G Y'
R
      + L × R - 33
¯33
      (+ L ×) R - 33
¯33
      + L (× R -) 33
33

      )ERASE L R

⍝ ==================================

      )ERASE A B C D

