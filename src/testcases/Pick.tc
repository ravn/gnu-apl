⍝ Pick.tc

⍝ ----------------------------------

      R←'FOUR' 'TO' 'GO'
      ≡R
2

      ⎕IO←1
      Z←2⊃R
      Z
TO

      ρZ
2

      ≡Z
1


      ⎕IO←0
      1⊃R
TO

      ⎕IO←1


      A←'S' 'SI' ('SIR' 'SIRE')
      ≡A
3


      W←2⊃A
      W
SI

      ρW
2

      ≡W
1


      X←3⊃A
      X
 SIR SIRE 

      ρX
2

      ≡X
2

⍝ pick from higher rank array ----------------------------------

      C←2 2ρ'ONE' 'TWO' 'BUCKLE' ('MY' 'SHOE')
      4⎕CR C
┏→━━━━━━━━━━━━━━━━━━━━━┓
↓┏→━━┓    ┏→━━┓        ┃
┃┃ONE┃    ┃TWO┃        ┃
┃┗━━━┛    ┗━━━┛        ┃
┃┏→━━━━━┓ ┏→━━━━━━━━━━┓┃
┃┃BUCKLE┃ ┃┏→━┓ ┏→━━━┓┃┃
┃┗━━━━━━┛ ┃┃MY┃ ┃SHOE┃┃┃
┃         ┃┗━━┛ ┗━━━━┛┃┃
┃         ┗∊━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━┛

      ρC
2 2
      ≡C
3

      Y←(⊂2 2)⊃C
      Y
 MY SHOE 

      ρY
2

      ≡Y
2


      D←C,[.5]2 2ρ'THREE' 'FOUR' 'SHUT' ('THE' 'DOOR')
      4⎕CR D
┏→━━━━━━━━━━━━━━━━━━━━━━┓
↓┏→━━┓    ┏→━━┓         ┃
┃┃ONE┃    ┃TWO┃         ┃
┃┗━━━┛    ┗━━━┛         ┃
┃┏→━━━━━┓ ┏→━━━━━━━━━━┓ ┃
┃┃BUCKLE┃ ┃┏→━┓ ┏→━━━┓┃ ┃
┃┗━━━━━━┛ ┃┃MY┃ ┃SHOE┃┃ ┃
┃         ┃┗━━┛ ┗━━━━┛┃ ┃
┃         ┗∊━━━━━━━━━━┛ ┃
┃                       ┃
┃┏→━━━━┓  ┏→━━━┓        ┃
┃┃THREE┃  ┃FOUR┃        ┃
┃┗━━━━━┛  ┗━━━━┛        ┃
┃┏→━━━┓   ┏→━━━━━━━━━━━┓┃
┃┃SHUT┃   ┃┏→━━┓ ┏→━━━┓┃┃
┃┗━━━━┛   ┃┃THE┃ ┃DOOR┃┃┃
┃         ┃┗━━━┛ ┗━━━━┛┃┃
┃         ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━━┛

      ρD
2 2 2

      ≡D
3

      Q←(⊂2 1 2)⊃D
      Q
FOUR

      ρQ
4

      ≡Q
1

⍝ specifying the left argument ----------------------------------

      Q←'FLY' 'PAPER'
      2 4⊃Q
E

      S←2 3ρ'AB' 'CD' 'EF' 'GH' 'IJ' 'KL'
      S
 AB CD EF 
 GH IJ KL 

      (1 3) 2⊃S
F


      M←'B' 'BA' ('BAT' 'BATH')
      M
 B BA   BAT BATH  
      ≡M
3

      N←(ι0)⊃M
      N
 B BA   BAT BATH  
      ≡N
3

      A←(ι0)⊃0 2ρ0
      ρA
0 2

      ≡A
1

⍝ additional examples ----------------------------------

      H←2 2ρ'BUCKS' 'TWANG' 'LYMPH' 'FROZE'
      4⎕CR  H
┏→━━━━━━━━━━━━━━┓
↓┏→━━━━┓ ┏→━━━━┓┃
┃┃BUCKS┃ ┃TWANG┃┃
┃┗━━━━━┛ ┗━━━━━┛┃
┃┏→━━━━┓ ┏→━━━━┓┃
┃┃LYMPH┃ ┃FROZE┃┃
┃┗━━━━━┛ ┗━━━━━┛┃
┗∊━━━━━━━━━━━━━━┛

      ≡H
2
      S←(2 1) 4⊃H
      S
P

      ≡S
0


      G←'I' 'AM' ('FOR' 'APL2')
      4⎕CR  G
┏→━━━━━━━━━━━━━━━━━━━━┓
┃I ┏→━┓ ┏→━━━━━━━━━━━┓┃
┃  ┃AM┃ ┃┏→━━┓ ┏→━━━┓┃┃
┃  ┗━━┛ ┃┃FOR┃ ┃APL2┃┃┃
┃       ┃┗━━━┛ ┗━━━━┛┃┃
┃       ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━┛
     
      ≡G
3

      T←3 2⊃G
      T
APL2

      ρT
4

      ≡T
1

      3 2 1⊃G
A


      E←2 3ρ'CRY' 'VOX' 'KID' 'JAB' (2 3ρι6) ('LEG' 'NTH')
      4⎕CR  E
┏→━━━━━━━━━━━━━━━━━━━━━━━━━━┓
↓┏→━━┓ ┏→━━┓   ┏→━━┓        ┃
┃┃CRY┃ ┃VOX┃   ┃KID┃        ┃
┃┗━━━┛ ┗━━━┛   ┗━━━┛        ┃
┃                           ┃
┃┏→━━┓ ┏→━━━━┓ ┏→━━━━━━━━━━┓┃
┃┃JAB┃ ↓1 2 3┃ ┃┏→━━┓ ┏→━━┓┃┃
┃┗━━━┛ ┃4 5 6┃ ┃┃LEG┃ ┃NTH┃┃┃
┃      ┗━━━━━┛ ┃┗━━━┛ ┗━━━┛┃┃
┃              ┗∊━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━━━━━━┛

      (2 2) (2 3)⊃E
6

      ρE
2 3

      ≡E
3


      U←(2 3) 2⊃E
      U
NTH

      ≡U
1

      J←(2 3) 2 3⊃E
      J
H

      ≡J
0


      K←'ELM' 'TAX' 'SPY' 'JOB' 'WIN'
      K←2 3ρK,⊂(2 3ρ'QUE' 'ZiG' 'HaD' 'FoR')
      4⎕CR  K
┏→━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
↓┏→━━┓ ┏→━━┓ ┏→━━┓              ┃
┃┃ELM┃ ┃TAX┃ ┃SPY┃              ┃
┃┗━━━┛ ┗━━━┛ ┗━━━┛              ┃
┃                               ┃
┃┏→━━┓ ┏→━━┓ ┏→━━━━━━━━━━━━━━━━┓┃
┃┃JOB┃ ┃WIN┃ ↓┏→━━┓ ┏→━━┓ ┏→━━┓┃┃
┃┗━━━┛ ┗━━━┛ ┃┃QUE┃ ┃ZiG┃ ┃HaD┃┃┃
┃            ┃┗━━━┛ ┗━━━┛ ┗━━━┛┃┃
┃            ┃┏→━━┓ ┏→━━┓ ┏→━━┓┃┃
┃            ┃┃FoR┃ ┃QUE┃ ┃ZiG┃┃┃
┃            ┃┗━━━┛ ┗━━━┛ ┗━━━┛┃┃
┃            ┗∊━━━━━━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛


      ρK
2 3

      ≡K
3


      P←(2 3)(1 2)⊃K
      P
ZiG
      ≡P
1

      I←(2 3)(1 2) 3⊃K
      I
G

      ≡I
0

⍝ selective spec ----------------------------------

      B←'P' 'PI' ('PIE' 'PIER')
      B
 P PI   PIE PIER  

      4⎕CR  B
┏→━━━━━━━━━━━━━━━━━━━━┓
┃P ┏→━┓ ┏→━━━━━━━━━━━┓┃
┃  ┃PI┃ ┃┏→━━┓ ┏→━━━┓┃┃
┃  ┗━━┛ ┃┃PIE┃ ┃PIER┃┃┃
┃       ┃┗━━━┛ ┗━━━━┛┃┃
┃       ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━┛

      (2⊃B)←'MY'
      B
 P MY   PIE PIER  

      4⎕CR  B
┏→━━━━━━━━━━━━━━━━━━━━┓
┃P ┏→━┓ ┏→━━━━━━━━━━━┓┃
┃  ┃MY┃ ┃┏→━━┓ ┏→━━━┓┃┃
┃  ┗━━┛ ┃┃PIE┃ ┃PIER┃┃┃
┃       ┃┗━━━┛ ┗━━━━┛┃┃
┃       ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━┛

      (2 1⊃B)←'TR'
      B
 P   TR Y    PIE PIER  

      4⎕CR  B
┏→━━━━━━━━━━━━━━━━━━━━━━━━┓
┃P ┏→━━━━━┓ ┏→━━━━━━━━━━━┓┃
┃  ┃┏→━┓ Y┃ ┃┏→━━┓ ┏→━━━┓┃┃
┃  ┃┃TR┃  ┃ ┃┃PIE┃ ┃PIER┃┃┃
┃  ┃┗━━┛  ┃ ┃┗━━━┛ ┗━━━━┛┃┃
┃  ┗∊━━━━━┛ ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━━━━┛

      (3 2 1⊃B)←'T'
      B
 P   TR Y    PIE TIER  

      4⎕CR B
┏→━━━━━━━━━━━━━━━━━━━━━━━━┓
┃P ┏→━━━━━┓ ┏→━━━━━━━━━━━┓┃
┃  ┃┏→━┓ Y┃ ┃┏→━━┓ ┏→━━━┓┃┃
┃  ┃┃TR┃  ┃ ┃┃PIE┃ ┃TIER┃┃┃
┃  ┃┗━━┛  ┃ ┃┗━━━┛ ┗━━━━┛┃┃
┃  ┗∊━━━━━┛ ┗∊━━━━━━━━━━━┛┃
┗∊∊━━━━━━━━━━━━━━━━━━━━━━━┛

⍝ ==================================

      )SI

      )ERASE B

      )CHECK
OK      - no stale functions
OK      - no stale values
OK      - no stale indices
OK      - no duplicate parents

⍝     ]OWNERS

