⍝ Take.tc
⍝ ----------------------------------

      3↑34 12 73 53 41
34 12 73

      ¯3↑34 12 73 53 41
73 53 41

⍝ nonscalar right argument ----------------------------------

      Y←4 5ρ'TRIADFIELDMOOSEDINER'
      Y
TRIAD
FIELD
MOOSE
DINER

      ¯2 3↑Y
MOO
DIN

      W←3 3 4ρ'BEATMYTHANTETONEMEANHEREUPONWEEKDOES'
      W
BEAT
MYTH
ANTE

TONE
MEAN
HERE

UPON
WEEK
DOES

      V←¯1 ¯2 2
      Z←V↑W
      Z
WE
DO

      ρZ
1 2 2

⍝ overtake ----------------------------------

      5↑21 33 52
21 33 52 0 0

      ¯5↑21 33 52
0 0 21 33 52

      5↑'RED'
RED  

      ¯5↑'RED'
  RED


      U←2 3ρι6
      U
1 2 3
4 5 6

      H←4 4↑U
      ρH
4 4
      H
1 2 3 0
4 5 6 0
0 0 0 0
0 0 0 0

      N←(1 2) (3 4)
      4↑N
 1 2  3 4  0 0  0 0 


      ¯6↑'A' 1 'B' 2
  A 1 B 2

      ¯6↑1 'A' 2 'B'
0 0 1 A 2 B

      3↑ι0
0 0 0

      2 3↑0 2ρ⊂0 0 0
 0 0 0  0 0 0  0 0 0 
 0 0 0  0 0 0  0 0 0 

⍝  scalar right argument ----------------------------------

      E←1↑2
      ρE
1

      E
2


      F←(ι0)↑2
      F
2

      ρF



      G←1 1 1↑2
      G
2
      ρG
1 1 1

      2 3↑2
2 0 0
0 0 0

⍝ effect on depth ----------------------------------

      T←'T' 'TO' ('TOT' 'TOTE')

      J←1↑T
      J
T

      ≡J
1

      K←2↑T
      K
 T TO 
      ≡K
2


      S←8 ((6 5) (4 3))
      S
 8   6 5  4 3  
      ≡S
3

      3↑S
 8   6 5  4 3   0 

      Q←⌽S
      Q
  6 5  4 3   8 

      3↑Q
  6 5  4 3   8   0 0  0 0  

⍝ selective spec ----------------------------------

      P←'ABCDE'
      (2↑P)←1 2
      P
1 2 CDE


      KY←3 4ρ'ABCDEFGHIJKL'
      KY
ABCD
EFGH
IJKL

      (¯2 1↑KY)←1 2
      KY
A BCD
1 FGH
2 JKL

⍝ ==================================
