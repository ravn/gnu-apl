⍝ Take_with_axis.tc
⍝ ----------------------------------

      ⎕IO←1 ⍝ for proper axis


      A←3 5ρ'GIANTSTORETRAIL'
      A
GIANT
STORE
TRAIL

      2↑[1]A
GIANT
STORE

      2 5↑A
GIANT
STORE

      ¯3↑[2]A
ANT
ORE
AIL

      3 ¯3↑A
ANT
ORE
AIL

⍝ overtake ----------------------------------

      B←2 3ρι6
      B
1 2 3
4 5 6

      3↑[1]B
1 2 3
4 5 6
0 0 0

      H←2 3ρ'ABCDEF'
      H
ABC
DEF

      Z←¯4↑[1]H
      Z
   
   
ABC
DEF

      ρZ
4 3


      C←2 3ρ1'A' 3 4 5 6
      C
1 A 3
4 5 6

      4↑[1]C
1 A 3
4 5 6
0   0
0   0

⍝ check that: L↑[X]R ←→ ⊃[X](⊂L)↑¨⊂[X]R

      L←4 ◊ X←1 ◊ R←C
      ⊃[X](⊂L)↑¨⊂[X]R
1 A 3
4 5 6
0   0
0   0

⍝ permitted axes ----------------------------------

      K←3 3 4ρ'HEROSHEDDIMESODABOARPARTLAMBTOTODAMP'
      K
HERO
SHED
DIME

SODA
BOAR
PART

LAMB
TOTO
DAMP

      ¯1 3↑[1 3]K
LAM
TOT
DAM

      ¯1 3↑[3 1]K
O
D
E

A
R
T

B
O
P

⍝ effect on depth ----------------------------------

      T←'D' 'DO'('DON' 'DONE') 'M' 'ME'('MEN' 'MENE')

      S←2 3ρT
      S
 D DO   DON DONE  
 M ME   MEN MENE  

      ≡S
3

      H←2↑[2]S
      H
 D DO 
 M ME 
      ≡H
2

      J←1↑[1]S
      J
 D DO   DON DONE  
      ≡J
3


      M←2 3ρ1(2 3)((4 5)(6 7))8(9 1)((2 3)(4 5))
      M
 1  2 3   4 5  6 7  
 8  9 1   2 3  4 5  

      ρM
2 3

      3↑[1]M
 1  2 3   4 5  6 7  
 8  9 1   2 3  4 5  
 0  0 0   0 0  0 0  

      T←1⌽[2]M
      T
 2 3   4 5  6 7   1 
 9 1   2 3  4 5   8 

      3↑[1]T
 2 3   4 5  6 7   1 
 9 1   2 3  4 5   8 
 0 0   0 0  0 0   0 

⍝ selective spec ----------------------------------

      U←3 4ρ'ABCDEFGHIJKL'
      U
ABCD
EFGH
IJKL

      (¯2↑[2]U)←3 2ρι6
      U
AB 1 2
EF 3 4
IJ 5 6

⍝ ==================================

